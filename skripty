01 suma srazek = 

-- scenar1: Respektuji filtry vizualu, zadne nerusim, nepridavam. Pouzivam zakladni agregacni funkce
-- typu SUM(),MIN()
SUM(data[srazky])

__________________________

02 suma srazek ruzyne = 
-- suma srazek v danem roce v lokalite ruzyne

-- scenar2: Respektuji filtry vizualu a nastavuji dalsi, tj. pouziji kombinaci funkci
-- SUMX(FILTER()), MINX(FILTER()), MAXX(FILTER()) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    --krok1: Funkce FILTER() prebira tabulku data se vsemi jejimi filtry
        data,
        --krok2: Funkce FILTER() nastavuje filtr na sloupec lokalita
        data[lokalita] = "RUZYNE"),
    --krok4: Funkce SUMX() agreguje sloupec srazky tabulky vracene funkci FILTER()
    data[srazky])
    
    ___________________________________
    
    03 suma razek vsechny roky = 

-- scenar3: Rusim filtry vizualu, zadne nenastavuji, tj. pouziji kombinaci funkci
-- SUMX(ALL()),MINX(ALL()) apod.

SUMX(
    --krok1: Funkce ALL() vytvari kopii tabulky data se vsemi jejimi hodnotami,
    -- tj. tabulka neni filtrovana
    ALL(data),
--krok2: Funkce SUMX() scita sloupec srazky tabulky vracene funkci ALL()
data[srazky])

_________________________________________________


04 suma srazek rok 2000 = 

-- scenar4: Rusim filtry vizualu a nastavuji si vlastni, pouziji kombinaci funkci
-- SUMX(FILTER(ALL())), MINX(FILTER(ALL())) apod.

SUMX(
    --krok3: Funkce FILTER() vraci tabulku
    FILTER(
    -- krok1: Funkce ALL() vytvari kopii tabulky data zbavenou vsech filtru
        ALL(data),
    --krok2: Funkce FILTER() nastavuje filtr na sloupec rok tabulky vracene funkci ALL()
    data[rok] = 2000),
--krok4: Funkce SUMX() agreguje sloupec srazky tabulky vracene funkci FILTER()
data[srazky])

_______________________________


05 suma srazek predchozi rok = 
-- scenar 4
SUMX(
    -- krok2: Funkce FILTER() nastavuje filtr na tabulku vracenou funkci ALL()
    FILTER(
        --krok1: Funkce ALL() vraci kopii tabulky data zbavenou vsech filtru
        ALL(data),
    -- krok 3: Vypocet hodnoty, na kterou filtrujeme. Hodnotu
    -- pocitame z originalni (vizualem filtrovane) tabulky data
    data[rok] = MAX(data[rok])-1),
-- krok4: Agregace sloupce srazky tabulky vracene funkci FILTER()
data[srazky])

___________________________________________

06 suma srazek sedmy mesic = 
SUMX(
    FILTER(data,data[mesic] = 7),
data[srazky])

___________________________________________

07 maximalni srazky v danem roce = 
-- tj. jaka je maximalni hodnota ze sloupce srazky
-- v danem roce
max(data[srazky])

_______________________________________________

08 prvni den maximalnich srazek = 
-- v danem roce

MINX(
    FILTER(data,
    data[srazky] = MAX(data[srazky])),
data[datum])

____________________________________________________

09 datumy maximalnich srazek = 
CONCATENATEX(
    FILTER(data,
    data[srazky] = MAX(data[srazky])),
data[datum],", ")
_____________________________________________________
10 pocet tropickych dnu ruzyne = 

-- v danem roce v lokalite ruzyne, maximalni_teplota > 30
MAX(0,
COUNTX(
    FILTER(data,data[lokalita] = "RUZYNE"
    && data[maximalni_teplota] > 30),
data[datum]))

_________________________________________________________

11 suma srazek mezirocne = 

-- suma srazek v danem roce
SUM(data[srazky])
-- MÍNUS
-
-- suma srazek v predchozim roce
SUMX(
    FILTER(
        ALL(data),
    data[rok] = MAX(data[rok]) - 1),
data[srazky])
__________________________________________________________________

12  pocet dnu ruzyne = 
-- pocet hodnot ze sloupce datum tabulky data filtrovane 
-- pres lokalitu na RUZYNE. Respektujeme filtr vizualu
COUNTX(
    FILTER(data,data[lokalita] = "RUZYNE"),
data[datum])

_____________________________________________________________________

13 maximalni teplota v danem roce v ruzyne = 

MAXX(
    FILTER(data,
    data[lokalita] = "RUZYNE"),
data[maximalni_teplota])

_________________________________________________________________________

14 minimalni teplota = 
-- pocítame hodnotu pouze pro total, tj. 
-- na radcich zobrazujeme NIC
IF(ISFILTERED(data[rok]), BLANK(),
MIN(data[minimalni_teplota]))

_______________________________________________

15 nejteplejsi lokality = 
-- lokality, ktere v danem roce vykazaly maximum z maximalnich teplot

CONCATENATEX(
    FILTER(data,data[maximalni_teplota] = MAX(data[maximalni_teplota])),
data[lokalita],", ")

___________________________________________________

16 suma srazek kumulativne = 
SUMX(
    FILTER(
        ALL(data),
    data[rok] <= MAX(data[rok])),
data[srazky])

_______________________________________________________________

17 prvni datum ruzyne = 
-- v danem roce 
MINX(
    FILTER(data,
    data[lokalita] = "RUZYNE"),
data[datum])

____________________________________________________________

18 suma srazek v danem roce a mesici = 
SUM(data[srazky])


_______________________________________________

19 suma srazek ytd = 
-- kumulativne v danem roce

SUMX(
    FILTER(ALL(data),
    data[rok] = MAX(data[rok])
    && data[mesic] <= MAX(data[mesic])),
data[srazky])

______________________________________________________
20 suma srazek ruzyne = 
-- v danem mesici, roce

SUMX(
    FILTER(data,data[lokalita] = "RUZYNE"),
data[srazky])

________________________________

cv02 minimalni datum vse = 

MINX(
    ALL(data),
data[datum])

__________________________________________

cv03 minimalni datum ruzyne = 
MINX(
    FILTER(data,data[lokalita] = "RUZYNE"),
data[datum])

_______________________________________________

cv05 min datum predchozi rok = 

MINX(
-- stejny mesic
FILTER(
    -- kopie tabulky data
    ALL(data),
    data[mesic] = MAX(data[mesic])
    && data[rok] = MAX(data[rok]) - 1) ,
data[datum])

____________________________________

cv11 podil srazek lokalita = 


-- suma srazek v danem roce a lokalite
SUM(data[srazky])
-- DELENO
/
-- suma srazek v danem roce, vsech lokalitach
SUMX(
    FILTER(
        ALL(data),
    data[rok] = MAX(data[rok])),
data[srazky])

__________________________________

cv12 maximalni srazky = MAX(data[srazky])

____________________________________


cv13 maximalni datum maximalnich srazek = 

-- v danem roce, lokalite

MAXX(
    FILTER(
        data,
    data[srazky] = MAX(data[srazky])),
data[datum])

___________________________________________

cvbonus3 datumy max srazek = 
CONCATENATEX(
    FILTER(data,
    data[maximalni_teplota] = MAX(data[maximalni_teplota])),
data[datum],", ",data[datum],ASC)

___________________________________________________

25 suma srazek kumulativne = 
-- ve vybranem roce, lokalitach

/*
Míru dosazujeme do vizuálu filtrovaného přes sloupce
rok,mesic,lokalita.
Chceme upravit filtr nad měsícem, tj. scénář 4
*/
SUMX(
    -- krok2: Funkce FILTER() nastavuje filtry
    -- na sloupce tabulky vrácené funkcí ALL()
    FILTER(
        --krok1: Funkce ALL() vrátí kopii
        -- tabulky data zbavenou všech filtrů.
        ALL(data),
    -- krok2: Rok tabulky vrácené funkcí ALL()
    -- se rovná vybranému roku z originální tabulky data
    -- filtrované vizuálem
    data[rok] = SELECTEDVALUE(data[rok])
    && data[mesic] <= SELECTEDVALUE(data[mesic])
    --krok2: Lokalita z tabulky vrácené funkcí ALL()
    -- se rovná jedné z lokalit vrácené funkcí VALUES(),
    -- která vrací unikátní hodnoty ze sloupce lokalita,
    -- tabulky data filtrované vizuálem
    && data[lokalita] IN VALUES(data[lokalita])),
-- krok3: Funkce SUMX() sčítá hodnoty ze sloupce srazky
-- tabulky vrácené funkcí FILTER()
data[srazky])


____________________________________________________________


28 pf barva sloupce srazky mezirocne = 

-- suma srazek v danem roce, mesici, lokalite
SUM(data[srazky])
-
-- MÍNUS
-- suma srazek v predchozim roce, danem mesici a lokalite
SUMX(
    FILTER(
        ALL(data),
    data[rok] = SELECTEDVALUE(data[rok])-1
    && data[mesic] = SELECTEDVALUE(data[mesic])
    && data[lokalita] IN VALUES(data[lokalita])),
data[srazky])

